plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'maven-publish'
}

group 'YAPION'
def groupID = 'yoyosource'
version 'SNAPSHOT'

ext {
    uberJarName = "${group}-${version}-all.jar"
    jarName = "${group}-${version}.jar"
    libs = "${buildDir}/libs"

    groupName = "${group}"

    publication = mayor + "." + minor + "." + patch

    releasePath = "releases/${groupID}/${group}"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'system.YAPIONMain'

repositories {
    jcenter()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.6'
    testCompileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.6'

    implementation 'org.atteo.classindex:classindex:3.4'
    testImplementation 'org.atteo.classindex:classindex:3.4'
    annotationProcessor 'org.atteo.classindex:classindex:3.4'
    testAnnotationProcessor 'org.atteo.classindex:classindex:3.4'

    implementation 'org.objenesis:objenesis:3.1'
    implementation 'org.slf4j:slf4j-api:2.0.0-alpha1'

    testCompile 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest:2.2'
}

shadowJar {
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }

    from(rootDir) {
        include 'LICENSE'
    }
    from(rootDir) {
        include '**/README.md'
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java/']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java/']
        }
    }
}

javadoc {
    source = 'src/main/java/yapion'
    // classpath = configurations.compile
    exclude("${rootDir}/src/main/java/system", "${rootDir}/src/main/java/test")
}

publishing {
    repositories {
        maven {
            url = "${rootDir}/releases"
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = groupID
            artifactId = group
            version = publication

            from components.java
        }
    }
}

task incrementMayor {
    description 'Increment Mayor'
    group "Release"

    doLast {
        mayor = Integer.parseInt(mayor) + 1
        minor = 0
        patch = 0
        println("Version: " + mayor + "." + minor + "." + patch)
        save()
    }
}

task incrementMinor {
    description 'Increment Minor'
    group "Release"

    doLast {
        minor = Integer.parseInt(minor) + 1
        patch = 0
        println("Version: " + mayor + "." + minor + "." + patch)
        save()
    }
}

task incrementPatch {
    description 'Increment Patch'
    group "Release"

    doLast {
        patch = Integer.parseInt(patch) + 1
        println("Version: " + mayor + "." + minor + "." + patch)
        save()
    }
}

private void save() {
    File f = new File("${new File("${buildDir}").parentFile.toString()}/gradle.properties")
    String[] strings = f.getText().split("\n")
    for (int i = 0; i < strings.length; i++) {
        if (strings[i].startsWith("mayor = ")) {
            strings[i] = "mayor = " + mayor
        } else if (strings[i].startsWith("minor = ")) {
            strings[i] = "minor = " + minor
        } else if (strings[i].startsWith("patch = ")) {
            strings[i] = "patch = " + patch
        }
    }
    f.text = String.join("\n", strings)
}

task projectPublish {
    description 'Publish this repo to GitHub Maven'
    group "Release"

    dependsOn publishMavenPublicationToMavenRepository
}

task projectCommit {
    description 'Commit this repo to GitHub/origin'
    group "Release"

    doLast {
        def status = shell("git status")
        if (!status.contains("${releasePath}/${mayor}.${minor}.${patch}")) {
            println("No new Version detected")
            return
        }

        println("Adding new files to git")
        shell("git add ${releasePath}/${mayor}.${minor}.${patch}/")
        shell("git add gradle.properties")
        shell("git add ${releasePath}/maven-metadata.xml")
        shell("git add ${releasePath}/maven-metadata.xml.md5")
        shell("git add ${releasePath}/maven-metadata.xml.sha1")
        shell("git add ${releasePath}/maven-metadata.xml.sha256")
        shell("git add ${releasePath}/maven-metadata.xml.sha512")

        println("Creating new commit with name 'Maven Release ${mayor}.${minor}.${patch}'")
        shell("git commit -m \"Maven Release ${mayor}.${minor}.${patch}\"")
        println("Creating new tag with name 'V${mayor}.${minor}.${patch}'")
        shell("git tag \"V${mayor}.${minor}.${patch}\"")

        println("Should this commit be pushed? [Y/n]")
        if ("Yy".contains(System.in.read() + "")) {
            shell("git push")
            shell("git push --tags")
        } else {
            println("Use 'git push' and 'git push --tags' to manually push this to origin.")
        }
    }
}

task projectJavaDoc {
    description 'Create new JavaDoc for the existing sources'
    group "Release"

    dependsOn javadoc

    doLast {
        shell("rm -r docs")
        shell("mkdir docs")
        shell("cp -R build/docs/javadoc/* docs")
    }
}

/**
 * Executes a shell command and returns the stdout result.
 *
 * @param command the command to execute (cannot contain pipes)
 * @return the trimmed result from stdout, stderr and the exit value
 */
def shell(String command) {
    def proc = ['bash', '-c', command].execute()
    def out = new StringBuilder()
    def err = new StringBuilder()
    proc.waitForProcessOutput(out, err)

    return [out.toString().trim(), err.toString().trim(), proc.exitValue()]
}

task buildProject {
    description 'Build this repo'
    group "Build"

    dependsOn 'build'

    doLast {
        if ("${buildDir}" == null) {
            return
        }
        delete fileTree("${libs}").matching {
            exclude("${uberJarName}")
        }
        file(libs + "/" + uberJarName).renameTo(file(libs + "/" + jarName))
    }
}

task licenseProject {
    description 'Add the License to every file'
    group "Build"

    mustRunAfter classes

    doLast {
        def command = "java -classpath ${buildDir}/classes/java/main system.LicenseSystem${groupName} ${buildDir}"
        def retval = command.execute().text
        print(retval)
    }
}
shadowJar.dependsOn licenseProject