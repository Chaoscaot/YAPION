plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.0.0'

    id 'maven-publish'
}

group 'YAPION'
version 'SNAPSHOT'

ext {
    uberJarName = "${group}-${version}-all.jar"
    jarName = "${group}-${version}.jar"
    libs = "${buildDir}/libs"

    groupName = "${group}"

    publication = mayor + "." + minor + "." + patch
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'system.YAPIONMain'

repositories {
    jcenter()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.6'
    testCompileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.6'

    implementation 'org.atteo.classindex:classindex:3.4'
    testImplementation 'org.atteo.classindex:classindex:3.4'
    annotationProcessor 'org.atteo.classindex:classindex:3.4'
    testAnnotationProcessor 'org.atteo.classindex:classindex:3.4'

    implementation 'org.objenesis:objenesis:3.1'
    implementation 'org.slf4j:slf4j-api:2.0.0-alpha1'

    testCompile 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest:2.2'
}

shadowJar {
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }

    from(rootDir) {
        include 'LICENSE'
    }
    from(rootDir) {
        include '**/README.md'
    }
}

publishing {
    repositories {
        maven {
            url = "${rootDir}/releases"
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'yoyosource'
            artifactId = group
            version = publication

            from components.java
        }
    }
}

task incrementMayor {
    description 'Increment Mayor'
    group "Release"

    doLast {
        mayor = Integer.parseInt(mayor) + 1
        minor = 0
        patch = 0
        println("Version: " + mayor + "." + minor + "." + patch)
        save()
    }
}

task incrementMinor {
    description 'Increment Minor'
    group "Release"

    doLast {
        minor = Integer.parseInt(minor) + 1
        patch = 0
        println("Version: " + mayor + "." + minor + "." + patch)
        save()
    }
}

task incrementPatch {
    description 'Increment Patch'
    group "Release"

    doLast {
        patch = Integer.parseInt(patch) + 1
        println("Version: " + mayor + "." + minor + "." + patch)
        save()
    }
}

private void save() {
    File f = new File("${new File("${buildDir}").parentFile.toString()}/gradle.properties")
    String[] strings = f.getText().split("\n")
    for (int i = 0; i < strings.length; i++) {
        if (strings[i].startsWith("mayor = ")) {
            strings[i] = "mayor = " + mayor
        } else if (strings[i].startsWith("minor = ")) {
            strings[i] = "minor = " + minor
        } else if (strings[i].startsWith("patch = ")) {
            strings[i] = "patch = " + patch
        }
    }
    f.text = String.join("\n", strings)
}

task publishProject {
    description 'Publish this repo to GitHub Maven'
    group "Release"

    dependsOn publishMavenPublicationToMavenRepository
}

task commitProject {
    description 'Commit this repo to GitHub/origin'
    group "Release"

    doLast {
        shell("git add releases/yoyosource/YAPION/${mayor}.${minor}.${patch}/")
        shell("git add gradle.properties")
        shell("git add releases/yoyosource/YAPION/maven-metadata.xml")
        shell("git add releases/yoyosource/YAPION/maven-metadata.xml.md5")
        shell("git add releases/yoyosource/YAPION/maven-metadata.xml.sha1")
        shell("git add releases/yoyosource/YAPION/maven-metadata.xml.sha256")
        shell("git add releases/yoyosource/YAPION/maven-metadata.xml.sha512")
        shell("git commit -m \"Maven Release ${mayor}.${minor}.${patch}\"")
        shell("git tag \"V${mayor}.${minor}.${patch}\"")
    }
}

/**
 * Executes a shell command and returns the stdout result.
 *
 * @param command the command to execute (cannot contain pipes)
 * @return the trimmed result from stdout, stderr and the exit value
 */
def shell(String command) {
    def proc = ['bash', '-c', command].execute()
    def out = new StringBuilder()
    def err = new StringBuilder()
    proc.waitForProcessOutput(out, err)

    return [out.toString().trim(), err.toString().trim(), proc.exitValue()]
}

task buildProject {
    description 'Build this repo'
    group "Build"

    dependsOn 'build'

    doLast {
        if ("${buildDir}" == null) {
            return
        }
        delete fileTree("${libs}").matching {
            exclude("${uberJarName}")
        }
        file(libs + "/" + uberJarName).renameTo(file(libs + "/" + jarName))
    }
}

task licenseProject {
    description 'Add the License to every file'
    group "Build"

    mustRunAfter classes

    doLast {
        def command = "java -classpath ${buildDir}/classes/java/main system.LicenseSystem${groupName} ${buildDir}"
        def retval = command.execute().text
        print(retval)
    }
}
shadowJar.dependsOn licenseProject